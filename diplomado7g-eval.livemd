# Entrega iOSLab diplomado

```elixir
Mix.install([
  {:req, "~> 0.4.8"},
  {:req_github_oauth, "~> 0.1.1"},
  {:kino_explorer, "~> 0.1.11"}
])
```

## Section

```elixir
inputs = [repo: Kino.Input.text("Repository")]
form = Kino.Control.form(inputs, submit: "Fetch")
```

```elixir
repo = "iOSLabUNAM/SwiftUIBasics"
req = Req.new(http_errors: :raise) |> ReqGitHubOAuth.attach()
```

```elixir
issues =
  1..4
  |> Task.async_stream(fn page ->
    Req.get!(req, url: "https://api.github.com/repos/#{repo}/issues?page=#{page}&per_page=50").body
  end)
  |> Stream.map(fn {:ok, res} -> res end)
  |> Enum.to_list()
  |> List.flatten()
  |> Enum.filter(fn iss -> is_nil(iss["closed_at"]) && is_nil(iss["pull_request"]) end)
  |> Enum.map(fn iss ->
    %{
      id: iss["number"],
      title: iss["title"]
    }
  end)
```

```elixir
prs =
  1..4
  |> Task.async_stream(fn page ->
    Req.get!(req, url: "https://api.github.com/repos/#{repo}/pulls?page=#{page}&per_page=50").body
  end)
  |> Stream.map(fn {:ok, res} -> res end)
  |> Enum.to_list()
  |> List.flatten()
  |> Enum.filter(fn pr -> is_nil(pr["closed_at"]) end)
```

```elixir
students =
  prs
  |> Enum.map(fn x -> x["user"]["url"] end)
  |> Enum.uniq()
  |> Task.async_stream(fn url ->
    res = Req.get!(req, url: url).body

    %{
      id: res["id"],
      name: res["name"],
      username: res["login"],
      email: res["email"],
      bio: res["bio"]
    }
  end)
  |> Enum.reduce([], fn {:ok, res}, acc -> [res | acc] end)
  |> Enum.uniq()
  |> Enum.filter(fn %{name: name} -> !is_nil(name) end)
```

```elixir
valid_prs =
  prs
  |> Enum.map(fn x ->
    %{
      id: x["number"],
      user_id: x["user"]["id"],
      author: nil,
      body: x["body"],
      url: String.replace(x["url"], "api.", "")
    }
  end)
  |> Enum.map(fn pr ->
    student = students |> Enum.find(fn x -> x.id == pr.user_id end)

    if is_nil(student) do
      pr
    else
      %{pr | author: student.name}
    end
  end)
  |> Enum.filter(fn %{body: body} -> !is_nil(body) end)
```

```elixir
eval =
  students
  |> Enum.map(fn student ->
    student_prs = Enum.filter(valid_prs, fn pr -> pr.user_id == student.id end)

    prs_row =
      issues
      |> Enum.map(fn issue ->
        student_pr =
          Enum.find(student_prs, fn %{body: body} -> String.contains?(body, "##{issue.id}") end)

        if is_nil(student_pr) do
          ""
        else
          student_pr.url
          |> String.replace("/repos/", "/")
          |> String.replace("/pulls/", "/pull/")
        end
      end)
      |> Enum.join(",")

    "#{student.name},#{prs_row}"
  end)
  |> Enum.join("\n")
```

```elixir
issues_head =
  issues
  |> Enum.map(fn %{id: id} -> "##{id}" end)
  |> Enum.join(",")

Explorer.DataFrame.load_csv!("nombre,#{issues_head}\n#{eval}")
```
